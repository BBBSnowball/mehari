buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'net.saliman:gradle-properties-plugin:1.2.0'
    }
}

// use properties plugin -> local settings (e.g. Xilinx path and version) are in gradle-local.properties
allprojects {
  apply plugin: 'properties'
}

def propertyOrDefault(propertyName, defaultValue) {
	return hasProperty(propertyName) ? project[propertyName] : defaultValue;
}

def defaultXilinxSettingsScript() {
	return "/opt/Xilinx/" + propertyOrDefault("xilinx_version", "14.6") + "/ISE_DS/settings64.sh"
}

def addMethodForAllTasks(methodName, methodClosure) {
	// executed for all current and future tasks: add a method using task.ext
	tasks.all { task ->
		task.ext[methodName] = methodClosure.curry(task)
	}
}

addMethodForAllTasks("environmentFromConfig") { task ->
	task.recommendedProperties names: ["xilinx_version", "xilinx_settings_script"]
	task.environment "XILINX_VERSION",         propertyOrDefault("xilinx_version", "14.6")
	task.environment "XILINX_SETTINGS_SCRIPT", propertyOrDefault("xilinx_settings_script", defaultXilinxSettingsScript())
}

task checkSystemDependencies(type:Exec) {
	description "Make sure that the system has everything we need."

	commandLine "./ci-0020-check-deps.sh"

	environmentFromConfig()
}

task prepareTools {
	dependsOn "tools:install", "tools:test"

	mustRunAfter checkSystemDependencies
}

subprojects {
	tasks.all { task ->
		task.mustRunAfter checkSystemDependencies
	}
}

task allCI(dependsOn: checkSystemDependencies)
defaultTasks "allCI"
