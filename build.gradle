buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'net.saliman:gradle-properties-plugin:1.2.0'
    }
}

// use properties plugin -> local settings (e.g. Xilinx path and version) are in gradle-local.properties
allprojects {
	apply plugin: 'properties'
	apply plugin: HelpersPlugin
}

task checkSystemDependencies(type:Exec) {
	description "Make sure that the system has everything we need."

	commandLine "./ci-0020-check-deps.sh"

	environmentFromConfig()
}

subprojects {
	tasks.all { task ->
		task.mustRunAfter checkSystemDependencies
	}
}

task prepareTools {
	dependsOn "tools:install", "tools:test"

	mustRunAfter checkSystemDependencies
}

forAllNormalTestTasks() { task ->
	task.dependsOn prepareTools
}

task allCI {
	dependsOn checkSystemDependencies
	dependsOn "single-pendulum-vhdl:test"
}
defaultTasks "allCI"

if (hasProperty("sispm_outlet") && sispm_outlet) {
	task powercycleBoard(type: Exec) {
		description "Turn the board off and on again."

		commandLine "./ci-0910-powercycle-board.sh"

		environment "SISPM_OUTLET", sispm_outlet
	}

	forAllNormalTestTasks {
		task.mustRunAfter powercycleBoard
	}

	allCI.dependsOn powercycleBoard
}
